# build with 'scons'
# see doc/build.txt

import sys
import os
import string


Default('phui examples')        # build everything by default


# find libraries
execfile('glut_libs.py')
FONT_LIBS = ['gltext', 'ttf']
Export('GLUT_LIBS FONT_LIBS')


# debug or release build?
CXXFLAGS = ['-Wall']
LINKFLAGS = []
if ARGUMENTS.get('debug'):
   CXXFLAGS.append('-g')
if ARGUMENTS.get('profile'):
   CXXFLAGS.append('-pg')
   LINKFLAGS.append('-pg')


# get install prefix for install target
PREFIX = '/usr/local'
if ARGUMENTS.get('prefix'):
   PREFIX = ARGUMENTS['prefix']
Export('PREFIX')

CXX = WhereIs('g++3') or 'g++'
LINK = CXX

# create the base environment
env = Environment(LIBPATH = '#phui',
                  CPPPATH = ['#', '#phui'],
                  ENV = os.environ,
                  CXX = CXX,
                  LINK = LINK,
                  LINKFLAGS = LINKFLAGS,
                  CXXFLAGS = CXXFLAGS)

Export('env')
SConscript(dirs = Split('phui examples test'))


# install target
env.Alias('install', PREFIX)
