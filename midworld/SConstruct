# build with 'scons'
import sys
import os
import string


# debug or release build?
CXXFLAGS = ['-Wall']
LINKFLAGS = []
if ARGUMENTS.get('debug'):
   CXXFLAGS.append('-g -DDEBUG')
else:
   CXXFLAGS.append('-DNDEBUG')

if ARGUMENTS.get('profile'):
   CXXFLAGS.append('-pg')
   LINKFLAGS.append('-pg')


# get install prefix for install target
PREFIX = '/usr/local'
if ARGUMENTS.get('prefix'):
   PREFIX = ARGUMENTS['prefix']
Export('PREFIX')


# link with external libraries
SDL_LIBS    = ['SDL']
GL_LIBS     = ['GL', 'GLU']
CORONA_LIBS = ['corona']
LIBS = SDL_LIBS + GL_LIBS + CORONA_LIBS


# create the base environment
env = Environment(LIBPATH = '#src',
                  CPPPATH = ['#', '#src'],
                  ENV = os.environ,
                  LINKFLAGS = LINKFLAGS,
                  CXXFLAGS = CXXFLAGS,
                  LIBS = LIBS)

Export('env')
SConscript(dirs = Split('src test'))


# install target
env.Alias('install', PREFIX)

# build all sources by default but not the tests
Default('src')
