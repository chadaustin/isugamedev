from os import path

Import('baseEnv PREFIX')
Import('SetupSDL SetupCorona SetupAudiere SetupGLText SetupOpenGL')

classes = Split("""
   Application
   BaseProjectileWeapon
   BoundsCollisionDetector
   Camera
   CollisionDesc
   CreditsState
   Enemy
   GameManager
   GameState
   IntroState
   Jukebox
   MenuState
   Player
   RigidBody
   State
   SoundEffectManager
   SoundManager
   Texture
   cubeGeometry
""")

sources = Split("""
    Main.cpp
""")

headers = Split("""
   CollisionDetector.h
   CollisionEvent.h
   CollisionListener.h
   Entity.h
   NullCollisionDetector.h
   SpatialIndex.h
   VectorSpatialIndex.h
   Weapon.h
""")

sources.extend(map(lambda n: n + '.cpp', classes))
headers.extend(map(lambda n: n + '.h', classes))

env = baseEnv.Copy()
env['LIBPATH'].extend(['#src/AI'])
env['LIBS'].extend(['ai'])
SetupSDL(env)
SetupCorona(env)
SetupAudiere(env)
SetupGLText(env)
SetupOpenGL(env)

programs = [
    env.Program(target = 'midworld', source = sources)
]

env.Install(path.join(PREFIX, 'bin'), programs)

SConscript(dirs = Split('AI'))
