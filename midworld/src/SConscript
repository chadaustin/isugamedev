from os import path

Import('baseEnv PREFIX BuildInfo')
Import('SetupSDL SetupCorona SetupAudiere SetupGLText SetupOpenSG SetupOpenGL')

# Listed here are interfaces that only have a .h associated with them
interfaces = Split("""
   BodyChangeListener
   CollisionDetector
   CollisionListener
   Entity
   SceneListener
   SceneViewer
   SpatialIndex
   Weapon
""")

# Listed here are classes that have a .h and a .cpp file associated with them
classes = Split("""
   AbstractEntity
   AmmoBag
   AmmoCrate
   Application
   BaseBullet
   BaseProjectileWeapon
   BodyChangeEvent
   BoundsSweepCollisionDetector
   Camera
   CollisionDesc
   CollisionResponse
   CreditsState
   Droid
   EndingState
   Enemy
   EntityFactory
   GameManager
   GameOverState
   GameScene
   GameState
   GunPickup
   HUD
   HomingMissile
   IntroState
   InputAction
   InputManager
   InputSymbol
   Jukebox
   LevelLoader
   LoadState
   MenuState
   MissileLauncher
   NavNodeTree
   OpenSGSceneViewer
   OptionsState
   Particle
   ParticleEngine
   PhysicsEngine
   Pistol
   Player
   ResourceManager
   RigidBody
   Scene
   SceneEvent
   Shotgun
   SkyDome
   SnowSystem
   State
   StateFactory
   SoundEffectManager
   SoundManager
   SpreadGun
   StoryState
   Testing
   Texture
   Turret
   UnsupportedState
   cubeGeometry
""")

buildInfo = BuildInfo()

# Listed here are headers that have no associated .cpp file
buildInfo.addHeaders(map(File, Split("""
   AssaultRifle.h
   BodyState.h
   CollisionEvent.h
   LokiTypeInfo.h
   UIDManager.h
   VectorSpatialIndex.h
   Version.h
""")))

# Listed here are sources that have no associated .h file
buildInfo.addSources(map(File, Split("""
   Main.cpp
   StateList.cpp
   Version.cpp
""")))

buildInfo.addExtraDist(map(File, Split("""
   SConscript
""")))

buildInfo.addHeaders(map(lambda n: File(n + '.h'),    interfaces))
buildInfo.addHeaders(map(lambda n: File(n + '.h'),    classes))
buildInfo.addSources(map(lambda n: File(n + '.cpp'),  classes))

env = baseEnv.Copy()

env ['LIBS'].extend(['Loom'])
env ['LIBS'].extend(['cal3d'])
env ['LIBS'].extend(['cppdom'])


SetupSDL(env)
SetupCorona(env)
SetupAudiere(env)
SetupGLText(env)
SetupOpenSG(env)
SetupOpenGL(env)

buildInfo.addPrograms([
    env.Program('../build/midworld', buildInfo.getSources())
])
env.Install(path.join(PREFIX, 'bin'), buildInfo.getPrograms())

Return('buildInfo')
